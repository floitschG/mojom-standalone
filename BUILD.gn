# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/cython/rules.gni")

group("python") {
  deps = [
    ":base",
    ":bindings",
    ":system",
    ":system_impl",
  ]
}

# GYP version: mojo.gyp:mojo_python_system
python_binary_module("system") {
  python_base_module = "mojo"
  configs = [ "../build/config:mojo_sdk" ]
  deps = [
    ":system_embedded",
  ]
}

python_binary_source_set("system_embedded") {
  cython_sources = [
    "mojo/c_async_waiter.pxd",
    "mojo/c_core.pxd",
    "mojo/c_export.pxd",
    "mojo/c_thunks.pxd",
    "mojo/system.pyx",
  ]
  configs = [ "../build/config:mojo_sdk" ]
  deps = [
    ":base",
    "../c/system",
    "../cpp/bindings:callback",
    "../cpp/system",
    "../cpp/utility",
    "../platform/native:system",
  ]
}

python_binary_module("system_impl") {
  python_base_module = "mojo"
  cython_sources = [
    "mojo/c_environment.pxd",
    "mojo/c_export.pxd",
    "mojo/c_thunks.pxd",
    "mojo/system_impl.pyx",
  ]
  sources = [
    "src/python_system_helper.cc",
    "src/python_system_helper.h",
  ]
  configs = [ "../build/config:mojo_sdk" ]
  deps = [
    ":base",
    ":python_common",
    "../c/environment",
    "../c/system",
    "../cpp/bindings:callback",
    "../cpp/environment:standalone",
    "../cpp/system",
    "../cpp/utility",
    "../platform/native:system",
  ]
}

python_binary_source_set("python_common") {
  sources = [
    "src/common.cc",
    "src/common.h",
  ]
  configs = [ "../build/config:mojo_sdk" ]
  deps = [
    "../c/environment:environment",
    "../cpp/bindings:callback",
    "../cpp/environment:environment",
    "../cpp/system:system",
    "../cpp/utility",
  ]
}

copy("base") {
  sources = [
    "mojo/__init__.py",
  ]
  outputs = [
    "$root_out_dir/python/mojo/{{source_file_part}}",
  ]
}

# GYP version: mojo.gyp:mojo_python_bindings
copy("bindings") {
  sources = [
    "mojo/bindings/__init__.py",
    "mojo/bindings/descriptor.py",
    "mojo/bindings/messaging.py",
    "mojo/bindings/promise.py",
    "mojo/bindings/reflection.py",
    "mojo/bindings/serialization.py",
  ]
  outputs = [
    "$root_out_dir/python/mojo/bindings/{{source_file_part}}",
  ]
  deps = [
    ":base",
    ":system",
  ]
}
