# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

template("code_generator") {
  assert(defined(invoker.lang))

  copy(target_name) {
    sources = [
      "${invoker.lang}_templates",
      "mojom_${invoker.lang}_generator.py",
    ]
    outputs = [
      "$root_out_dir/tools/bindings/generators/{{source_file_part}}",
    ]
  }
}

group("generators") {
  deps = [
    ":cpp",
    ":java",
    ":js",
    ":dart",
    ":python",
    ":go",
  ]
}

code_generator("cpp") {
  lang = "cpp"
}

code_generator("java") {
  lang = "java"
}

code_generator("js") {
  lang = "js"
}

code_generator("dart") {
  lang = "dart"
}

code_generator("python") {
  lang = "python"
}

code_generator("go") {
  lang = "go"
}
